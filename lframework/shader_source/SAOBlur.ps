#define EdgeSharpness (1.0)
#define Scale (2)
#define R (4)
#define Epsilon (0.0001)

static const float gaussian[R+1] = {0.224174, 0.192116, 0.120921, 0.0558982, 0.0189781,};

cbuffer ConstantBuffer0 : register(b0)
{
    int2 size : packoffset(c0.x); //width, height
    float2 axis : packoffset(c0.z);
}

struct VSOutput
{
    float4 position : SV_POSITION;
    float2 tex : TEXCOORD0;
};

Texture2D tex : register(t0);


float unpackZ(float2 v)
{
    return dot(v,float2(256.0/257.0, 1.0/257.0));
}

float4 main(VSOutput input) : SV_Target
{
    int2 screenSpace = input.tex * size;
    
    float4 c = tex.Load(int3(screenSpace, 0));

    float4 color;
    color.gb = c.gb;
    color.a = 1;

    //float key = unpackZ(c.gb);
    float key = c.g;
    float sum = c.r;
    if(key == 1.0){
        color.r = sum;
        return color;
    }

    float base = gaussian[0];
    float totalWeight = base;
    sum *= totalWeight;

    [unroll]
    for(int r=-R; r<=R; ++r){
        if(0 != r){
            int2 p = clamp(int2(screenSpace + axis * (r*Scale)), int2(0,0), size);
            c = tex.Load(int3(p, 0));
            //float tapKey = unpackZ(c.gb);
            float tapKey = c.g;
            float value = c.r;

            float weight = 0.3 + gaussian[abs(r)];
            weight *= max(0.0, 1.0 - (2000.0*EdgeSharpness) * abs(tapKey - key));
            sum += value * weight;
            totalWeight += weight;
        }
    }

    color.r = sum/(totalWeight + Epsilon);
    return color;
}
