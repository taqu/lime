#include "Common.scs"
#define R (4)
#define GRIDSIZE (256)
#ifdef NUMTHREADS
#undef NUMTHREADS
#endif
#define NUMTHREADS (GRIDSIZE+R*2)

static const float gaussian[R+1] = {0.398943, 0.241971, 0.0539911, 0.00443186, 0.000133831,};

Texture2D<float2> texIn : register(t0);
SamplerState samPoint : register(s0);
RWTexture2D<float2> texOut : register(u0);

//in D3D10 the maximum total size of all variables with the groupshared storage class is 16kb, in D3D11 the maximum size is 32kb
groupshared float2 sharedSamples[NUMTHREADS];

[numthreads(NUMTHREADS, 1, 1)]
void main(uint3 groupID : SV_GroupID, uint3 groupThreadID : SV_GroupThreadID)
{
    int width, height;
    texIn.GetDimensions(width, height);

    const int gridStartY = groupID.x * GRIDSIZE;
    const int gridY = (int)groupThreadID.x - R;

    const int sampleX = groupID.y;
    const int sampleY = gridStartY + gridY;
    int2 samplePosition = int2(sampleX, sampleY);

    float2 texcoord = saturate((samplePosition+0.5f)/float2(width,height));
    float2 depthBlur = texIn.SampleLevel(samPoint, texcoord, 0);
    sharedSamples[groupThreadID.x] = depthBlur;
    GroupMemoryBarrierWithGroupSync();

    if(0<=gridY && gridY<GRIDSIZE && sampleY<height){
        int groupThreadIDX = (int)groupThreadID.x; //[r GRIDSIZE)
        float blur = 0;
        float totalWeight = 0;
        [unroll]
        for(int r=-R; r<=R; ++r){
            int tap = groupThreadIDX + r;
            float2 sample = sharedSamples[tap];
            float depthWeight = (sample.x <= depthBlur.x);
            float blurWeight = saturate(sample.y - depthBlur.y);
            float weight = depthWeight * blurWeight;
            weight += (r==0)? 1 : 0;
            weight = saturate(weight);

            blur += sample.y * weight;
            totalWeight += weight;
        }
        blur /= totalWeight;
        texOut[samplePosition] = float2(depthBlur.x, blur);
    }
}
